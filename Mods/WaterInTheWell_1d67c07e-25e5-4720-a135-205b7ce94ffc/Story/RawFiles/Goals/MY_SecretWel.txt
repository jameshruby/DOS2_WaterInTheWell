Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_ShovelArea(TRIGGERGUID_S_SecretWell_Box_add32a6d-3178-4e36-92f2-ccd308a2518e,"CoS_Temples_SecretTunnel_Hole",ITEMGUID_S_SecretWell_DirtPile_585f21e7-dc9e-46e6-8209-7f7a2d345b5d);
DB_ShovelRewardItemSpawn("CoS_Temples_SecretTunnel_Hole",ITEMGUID_SecretWellEntrance_C_000_debb06c3-8bf8-4114-879e-acf76aaa7fd3);
DB_OneShotPlayerTrigger(TRIGGERGUID_BoxTrigger_OneShot_Wel_4f143454-54d5-46db-ba0f-e9aab17a79db);
DB_ElieCheckResurrected(CHARACTERGUID_Elie_Dead_c0481cce-ac8e-4507-99b5-8c962c3a66d1);
DB_Elie(CHARACTERGUID_Elie_Dead_c0481cce-ac8e-4507-99b5-8c962c3a66d1);

DB_QuestDef_State("ElsesTomb", "FindElsesBody", 1);
DB_QuestDef_State("ElsesTomb", "ElseRessurectedByThePlayer");
DB_QuestDef_State("ElsesTomb", "FightWithGraveMan");
DB_QuestDef_State("ElsesTomb", "BurryElse");
DB_QuestDef_State("ElsesTomb", "SomebodyIsAtTheDoor");
DB_QuestDef_State("ElsesTomb", "WeReturned", -1);




KBSECTION
IF
DB_IsPlayer(_Player)
THEN
ProcObjectTimer((GUIDSTRING)_Player, "GiveQuest", 1000);

PROC
ProcObjectTimerFinished(_Player, "GiveQuest")
THEN
PartySetFlag((CHARACTERGUID)_Player, "QuestUpdate_ElsesTomb_FindElsesBody");


IF //Kill Elie at the start
DB_ElieCheckResurrected(_Elie)
THEN
CharacterDieImmediate((CHARACTERGUID)_Elie, 0, "Physical"); 

IF //First interaction with the character will res her...
CharacterLootedCharacterCorpse(_Player, CHARACTERGUID_Elie_Dead_c0481cce-ac8e-4507-99b5-8c962c3a66d1)
AND
DB_IsPlayer(_Player)
AND
DB_ElieCheckResurrected(_Elie)
THEN
DisplayText(_Player, "Else! Else!");
CloseUI(_Player, "Container");
CharacterResurrect((CHARACTERGUID)_Elie);
FadeOutBlack((CHARACTERGUID)_Player, 0.3, "ElseFadeOut");

IF //Else resed, make her dead again(since that will trigger respawnable ghost effect)
CharacterResurrected(_Elie)
AND
DB_ElieCheckResurrected(_Elie)
AND
DB_IsPlayer(_Player)
THEN
CharacterRecruitCharacter((CHARACTERGUID)_Elie, (CHARACTERGUID)_Player);
CharacterAddToPlayerCharacter((CHARACTERGUID)_Elie, (CHARACTERGUID)_Player);
//Stops playing the torture sounds
SetOnStage(TRIGGERGUID_PointTrigger_TortureSounds_735133eb-b191-47f3-b043-bf74badfaa17, 0);
SetOnStage(TRIGGERGUID_PointTrigger_TortureSounds_000_c00e7971-28e3-45e6-b7f7-a6832c3f8c5f, 0);
CharacterDieImmediate((CHARACTERGUID)_Elie, 0, "Physical"); //Hang
NOT DB_ElieCheckResurrected(_Elie);
ProcObjectTimer(_Player, "KilledDelay", 500);

PROC
ProcObjectTimerFinished(_Player, "KilledDelay")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
DB_Elie(_Elie)
THEN
FadeIn((CHARACTERGUID)_Player, 0.3, "ElseFadeIn");
DisplayText(_Player, "Else! Else!");

IF //Else is ressed by player, turn her back to NPC, this will be running ONLY on the player res
CharacterUsedSkillOnTarget((CHARACTERGUID)_Player, (CHARACTERGUID)_Elie, "Teleportation_ResurrectScroll", _, _)
AND
DB_IsPlayer(_Player)
AND
DB_Elie(_Elie)
THEN
CharacterMakeNPC((CHARACTERGUID)_Elie);
CharacterAddToPlayerCharacter((CHARACTERGUID)_Elie, (CHARACTERGUID)_Player);
ProcObjectTimer(_Player, "ResedDelay", 2000);

PROC
ProcObjectTimerFinished(_Player, "ResedDelay")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
DB_Elie(_Elie)
THEN
CharacterFollowCharacter((CHARACTERGUID)_Elie, (CHARACTERGUID)_Player);
CharacterAddToPlayerCharacter((CHARACTERGUID)_Elie, (CHARACTERGUID)_Player);
CharacterSetAnimationOverride((CHARACTERGUID)_Elie, "");
PartySetFlag(_Player, "QuestUpdate_ElsesTomb_ElseRessurectedByThePlayer");
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "__Start"
