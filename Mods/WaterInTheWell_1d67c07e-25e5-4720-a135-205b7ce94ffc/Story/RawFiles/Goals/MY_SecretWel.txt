Version 1
SubGoalCombiner SGC_AND
INITSECTION
//Object holder DBs
DB_ShovelArea(TRIGGERGUID_S_SecretWell_Box_add32a6d-3178-4e36-92f2-ccd308a2518e,"CoS_Temples_SecretTunnel_Hole",ITEMGUID_S_SecretWell_DirtPile_585f21e7-dc9e-46e6-8209-7f7a2d345b5d);
DB_ShovelRewardItemSpawn("CoS_Temples_SecretTunnel_Hole",ITEMGUID_SecretWellEntrance_C_000_debb06c3-8bf8-4114-879e-acf76aaa7fd3);

DB_OneShotPlayerTrigger(TRIGGERGUID_BoxTrigger_OneShot_Wel_4f143454-54d5-46db-ba0f-e9aab17a79db);
DB_ElieCheckResurrected(CHARACTERGUID_Elie_Dead_c0481cce-ac8e-4507-99b5-8c962c3a66d1);
DB_Elie(CHARACTERGUID_Elie_Dead_c0481cce-ac8e-4507-99b5-8c962c3a66d1);

DB_McGuffin(ITEMGUID_TOOL_SignWall_A_Gold_ElseGraveMarker_485f8339-5a09-4e26-8f9f-fc5c0e649617);
DB_HouseDoorsItem(ITEMGUID_1m_CityHouse_A_Door_OurHouse_da7b63ff-0488-4e2d-a9e2-dfd5dfa0e22c);

//GraveHolders
DB_FlowerCupEmpty((ITEMGUID)ITEMGUID_CON_Flower_Cup_A_Empty_000_d5b4259f-aa65-4a6c-91cb-bbd0a418573e); 
//DB_FlowerCupFull((ITEMGUID)ITEMGUID_CON_Flower_Cup_A_Water_003_7b2dfe57-a645-4822-a324-236dd5f75fc8); 
DB_Kitty(KittyKatie_Ghost_9eadd18f-e2b2-41e0-bb9e-5bc9041316ec);

DB_ShovelArea(TRIGGERGUID_S_SecretWell_Box_add32a6d-3178-4e36-92f2-ccd308a2518e,"CoS_Temples_SecretTunnel_Hole",ITEMGUID_S_SecretWell_DirtPile_585f21e7-dc9e-46e6-8209-7f7a2d345b5d);
DB_ShovelRewardItemSpawn("CoS_Temples_SecretTunnel_Hole",ITEMGUID_SecretWellEntrance_C_000_debb06c3-8bf8-4114-879e-acf76aaa7fd3);


//Dialogues
DB_Dialogs(ITEMGUID_TOOL_SignWall_A_Gold_ElseGraveMarker_485f8339-5a09-4e26-8f9f-fc5c0e649617, "FoundSignificantItem");
DB_Dialogs(ITEMGUID_CEM_Tombstone_H_Dynamic_000_8ce72b2e-0b35-4f7c-8b7c-2958fbddeebb,"TombDialogue");
DB_Dialogs(KittyKatie_Ghost_9eadd18f-e2b2-41e0-bb9e-5bc9041316ec, "KittyKatie");
DB_Dialogs(CHARACTERGUID_S_Regimir_Antagonist_a27fe007-4abb-4885-b9a7-fcad88f48d3d, "RegimirDialogue");


//QuestStates
DB_QuestDef_State("ElsesTomb", "InvestigateDoors", 1);
DB_QuestDef_State("ElsesTomb", "FoundASignificantItem");
DB_QuestDef_State("ElsesTomb", "FindElsesBody");
DB_QuestDef_State("ElsesTomb", "ElseRessurectedByThePlayer");
DB_QuestDef_State("ElsesTomb", "FightWithGraveMan");
DB_QuestDef_State("ElsesTomb", "BurryElse");
DB_QuestDef_State("ElsesTomb", "SomebodyIsAtTheDoor");
DB_QuestDef_State("ElsesTomb", "WeReturned", -1);

DB_QuestDef_State("ElsesTomb", "FoundASignificantItem");


KBSECTION
 //REGION Init Elie
IF //Kill Elie at the start
DB_ElieCheckResurrected(_Elie)
THEN
DB_NOOP(1);
//CharacterDieImmediate((CHARACTERGUID)_Elie, 0, "Hang"); 
//END_REGION

//REGION On Init
IF
DB_IsPlayer(_Player)
AND
DB_McGuffin(_McGuffin)
THEN 
//Setting player status
CharacterUseItem(_Player, ITEMGUID_Helper_Sleep_A_000_39d428f8-084b-4112-98a3-d7d33bd09600, "");
ApplyStatus(_Player, "DRUNK", 180.0);

ProcObjectTimer(_Player, "SleepDelay", 5000);

//Create waterPuddle before the door
CreatePuddle(_McGuffin, "SurfaceWater", 5, 25, 5, 10, 0.02); ///2.0,-1.0);
ItemTemplateAddTo("Flower_Cup_A_Water_425c021a-0ddd-44e9-979f-5c90afb060c0", _Player, 1);
CharacterLevelUpTo(_Player, 10);
ItemTemplateAddTo("Scroll_Skill_Source_Resurrect_60180909-ee47-4d1c-b81c-e43bd8fdce1e", _Player, 1);

PROC
ProcObjectTimerFinished(_Player, "SleepDelay")
THEN
//TODO dialogue
Proc_StartDialog(1, "NarratorIntroduction", _Player);


IF
DialogEnded("NarratorIntroduction", _)
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
PartySetFlag(_Player, "QuestUpdate_ElsesTomb_InvestigateDoors");

//END_REGION

//REGION Weve been teleported outside, initiate dialog
IF
CharacterUsedItem(_Player, (ITEMGUID)_HouseDoors)
AND
DB_IsPlayer(_Player)
AND
DB_HouseDoorsItem(_HouseDoors)
AND
DB_McGuffin(_McGuffin)
THEN
CharacterAddSkill(_Player, "Shout_SpiritVision");
Proc_StartDialog(1, "FoundSignificantItem", _Player);


IF
DialogEnded("FoundSignificantItem", _)
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
DB_McGuffin(_McGuffin)
THEN
ItemToInventory((ITEMGUID)_McGuffin, _Player, 1);
PartySetFlag(_Player, "QuestUpdate_ElsesTomb_FoundASignificantItem");

//END_REGION

//REGION  Grave/Tomb interaction&FlowerWatering
IF
CharacterUsedItem(_Player, ITEMGUID_CEM_Tombstone_H_Dynamic_000_8ce72b2e-0b35-4f7c-8b7c-2958fbddeebb)
THEN
Proc_StartDialog(1, "TombDialogue", _Player);//ITEMGUID_BLD_Humans_Tomb_Lid_A_000_9abef75b-fbbd-4647-8da1-b6e074bdbd51

IF
DialogEnded("TombDialogue", _Id)
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
DB_FlowerCupEmpty(_EmptyCup)
AND
ItemTemplateIsInUserInventory(_Player, "Flower_Cup_A_Water_425c021a-0ddd-44e9-979f-5c90afb060c0", 0, _FullCupCount)
AND
_FullCupCount > 0
AND
GetItemForItemTemplateInInventory((CHARACTERGUID)_Player, "Flower_Cup_A_Water_425c021a-0ddd-44e9-979f-5c90afb060c0", _FullCup)
//DB_FlowerCupFull(_FullCup)
AND
GetPosition(_EmptyCup, _FlowerCupX, _FlowerCupY, _FlowerCupZ)
THEN
DisplayText(_Player, "Here you go, flowers!");
//ItemTemplateRemoveFrom((STRING)_ItemTemplate, (GUIDSTRING)_Object,(INTEGER)_Count)
ItemMoveToPosition(_FullCup, _FlowerCupX, _FlowerCupY, _FlowerCupZ, 5.0, 5.0, "");
DB_FlowerCupPos(_FlowerCupX, _FlowerCupY, _FlowerCupZ);
ItemToInventory(_EmptyCup, _Player);
NOT DB_FlowerCupEmpty(_EmptyCup);
DB_FlowerCupFull(_FullCup);
ProcObjectTimer(_Player, "KittyDelay", 2000);
//END_REGION

//REGION Kitty drank all the water
PROC
ProcObjectTimerFinished(_Player, "KittyDelay")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
DB_FlowerCupPos(_FlowerCupX, _FlowerCupY, _FlowerCupZ)
AND
CreateItemTemplateAtPosition("ITEMGUID_CON_Flower_Cup_A_Empty_8f68f678-5cf0-43cb-9428-2b8a87806774", _FlowerCupX, _FlowerCupY, _FlowerCupZ,(ITEMGUID)_EmptyCup)
AND
DB_Kitty(_KittyKatie)
AND
CharacterConsume((CHARACTERGUID)_KittyKatie, "ITEMGUID_CON_Flower_Cup_A_Water_425c021a-0ddd-44e9-979f-5c90afb060c0", _)
AND
DB_FlowerCupFull(_FullCup)
THEN
Proc_StartDialog(1, "Narrator", _Player);
//ItemTemplateAddTo("WPN_Tool_Pitchfork_2H_A be051a81-d08d-4fea-9fa4-f1c32049c13d", _Player, 1);
ItemRemove(_FullCup);
//CharacterUseItem((CHARACTERGUID)_KittyKatie, _FullCup, "");

NOT DB_FlowerCupFull(_FullCup);
DB_FlowerCupEmpty(_EmptyCup);
//END_REGION

//REGION TODO ADD JOURNAL ENTRY GET THE WATER
//END_REGION

//REGION TODO WE STEPPED INTO TRIGGER AND FOUND THE BODY
// PartySetFlag((CHARACTERGUID)_Player, "QuestUpdate_ElsesTomb_FindElsesBody");
//END_REGION

//REGION Reviving Else
IF //First interaction with the character will res her...
CharacterLootedCharacterCorpse(_Player, CHARACTERGUID_Elie_Dead_c0481cce-ac8e-4507-99b5-8c962c3a66d1)
AND
DB_IsPlayer(_Player)
AND
DB_ElieCheckResurrected(_Elie)
THEN
DisplayText(_Player, "Else! Else!");
CloseUI(_Player, "Container");
CharacterResurrect((CHARACTERGUID)_Elie);
FadeOutBlack((CHARACTERGUID)_Player, 0.3, "ElseFadeOut");

IF //Else resed, make her dead again(since that will trigger respawnable ghost effect)
CharacterResurrected(_Elie)
AND
DB_ElieCheckResurrected(_Elie)
AND
DB_IsPlayer(_Player)
THEN
TeleportTo(_Elie, TRIGGERGUID_PointTrigger_001_a5bdb7f8-749d-4dc0-a440-151723e93f1a, "", 1, 0, 1);
CharacterRecruitCharacter((CHARACTERGUID)_Elie, (CHARACTERGUID)_Player);
CharacterAddToPlayerCharacter((CHARACTERGUID)_Elie, (CHARACTERGUID)_Player);
//Stops playing the torture sounds
SetOnStage(TRIGGERGUID_PointTrigger_TortureSounds_735133eb-b191-47f3-b043-bf74badfaa17, 0);
SetOnStage(TRIGGERGUID_PointTrigger_TortureSounds_000_c00e7971-28e3-45e6-b7f7-a6832c3f8c5f, 0);
CharacterDieImmediate((CHARACTERGUID)_Elie, 0, "Physical"); //Hang
ProcObjectTimer(_Player, "KilledDelay", 500);
PROC
ProcObjectTimerFinished(_Player, "KilledDelay")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
DB_Elie(_Elie)
THEN
FadeIn((CHARACTERGUID)_Player, 0.3, "ElseFadeIn");
DisplayText(_Player, "Else! Else!");

IF //Else is ressed by player, turn her back to NPC, this will be running ONLY on the player res
CharacterUsedSkillOnTarget((CHARACTERGUID)_Player, (CHARACTERGUID)_Elie, "Teleportation_ResurrectScroll", _, _)
AND
DB_IsPlayer(_Player)
AND
DB_Elie(_Elie)
THEN
CharacterMakeNPC((CHARACTERGUID)_Elie);
CharacterAddToPlayerCharacter((CHARACTERGUID)_Elie, (CHARACTERGUID)_Player);
ProcObjectTimer(_Player, "ResedDelay", 2000);

PROC
ProcObjectTimerFinished(_Player, "ResedDelay")
AND
DB_IsPlayer((CHARACTERGUID)_Player)
AND
DB_Elie(_Elie)
THEN
ProcCharacterFollowCharacter((CHARACTERGUID)_Elie, (CHARACTERGUID)_Player);
PartySetFlag(_Player, "QuestUpdate_ElsesTomb_ElseRessurectedByThePlayer");
//Starting the dialogue
Proc_StartDialog(1, "ElieDialog", _Player);

IF
DialogEnded("ElieDialog", _)
AND
DB_IsPlayer((CHARACTERGUID)_Player)
THEN
//TODO add PartySetFlag(_Player, "QuestUpdate_ElsesTomb_ElseFindTheSoulJar");
DB_NOOP(1);

//END_REGION

//REGION We found Antagonist => FIGHT
IF
DialogEnded("RegimirDialogue",_)
THEN
SetFaction(CHARACTERGUID_S_Regimir_Antagonist_a27fe007-4abb-4885-b9a7-fcad88f48d3d, "Evil NPC");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "__Start"
